plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.2'
}

group = 'io.github.seerainer.hexedit'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
        vendor = JvmVendorSpec.GRAAL_VM
    }
}

repositories {
    mavenCentral()
}

ext {
    swtVersion = '3.131.0'
    swtArtifact = 'org.eclipse.swt.'
    os = System.getProperty("os.name").toLowerCase()
    arch = System.getProperty("os.arch")
    normalizedArch = arch
    swtWS = 'unknown'
    swtOS = 'unknown'
}

switch (os) {
    case 'linux':
        swtArtifact += 'gtk.linux.'
        swtWS = 'gtk'
        swtOS = 'linux'
        break
    case 'mac os x':
        swtArtifact += 'cocoa.macosx.'
        swtWS = 'cocoa'
        swtOS = 'macosx'
        break
    default:
        if (os.startsWith("win")) {
            swtArtifact += 'win32.win32.'
            swtWS = 'win32'
            swtOS = 'win32'
        }
}

switch (arch) {
    case ['amd64', 'x86_64']:
        swtArtifact += 'x86_64'
        normalizedArch = 'x86_64'
        break
    case 'aarch64':
        swtArtifact += 'aarch64'
        normalizedArch = 'aarch64'
        break
}

dependencies {
    implementation("org.eclipse.platform:${swtArtifact}:${swtVersion}") {
        exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
    }
    
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

application {
    mainClass.set('io.github.seerainer.hexedit.Main')
}

run {
    if (os.contains("mac")) {
        jvmArgs += '-XstartOnFirstThread'
    }
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

graalvmNative {
    testSupport = true
    toolchainDetection = true

    binaries {
        main {
            imageName = 'HexEdit'
            mainClass = application.mainClass
            buildArgs.add('-march=compatibility')
            buildArgs.add('-Ob')
            buildArgs.add('--enable-native-access=ALL-UNNAMED')
            if (os.contains('mac')) {
                buildArgs.add('-J-XstartOnFirstThread')
            }
            buildArgs.add('--no-fallback')
            buildArgs.add('-R:MinHeapSize=256m')
            buildArgs.add('-R:MaxHeapSize=1g')
            buildArgs.add('-R:MaxNewSize=512m')
            useFatJar = true
        }
    }
}

tasks.withType(Jar).configureEach {
    exclude(
        'META-INF/ECLIPSE*',
    	'META-INF/maven/**/**/*',
    	'META-INF/versions/9/*',
    	'META-INF/*.txt',
        'about_files/*',
        'version.txt',
        '.api_description',
        'about.html',
        'fragment.properties',
        'libswt*.jnilib',
        'libswt*.so',
        'swt*.dll',
        'WebView2Loader.dll',
        'module-info.class',
        'org/eclipse/swt/awt/*',
        'org/eclipse/swt/opengl/*'
    )
    manifest {
        attributes 'Main-Class': application.mainClass
        attributes 'SWT-WS': swtWS
        attributes 'SWT-OS': swtOS
        attributes 'SWT-Arch': normalizedArch
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform {
        includeTags 'unit', 'integration'
    }

    maxHeapSize = '4g'

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        showStandardStreams = true
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat = 'full'
    }

    systemProperties = [
        'junit.jupiter.execution.parallel.enabled': 'true',
        'junit.jupiter.execution.parallel.mode.default': 'concurrent',
        'junit.jupiter.execution.parallel.mode.classes.default': 'concurrent',
        'java.awt.headless': 'true'
    ]

    jvmArgs = [
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--enable-native-access=ALL-UNNAMED'
    ]
}

task unitTest(type: Test) {
    description = 'Runs unit tests'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'unit'
    }

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

task integrationTest(type: Test) {
    description = 'Runs integration tests'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'integration'
    }

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    timeout = Duration.ofMinutes(5)
}

check.dependsOn unitTest, integrationTest
build.dependsOn check

task allTests {
    description = 'Runs all test suites in sequence'
    group = 'verification'

    dependsOn unitTest, integrationTest

    tasks.findByName('integrationTest').mustRunAfter 'unitTest'
}

